<program> = atom*
<atom> = regex / string / nil / number / boolean / set / map / vector /
         qualified-keyword / keyword / qualified-identifier / identifier /
         binding-definition /
         fn-definition / do-definition /
         if /
         application

<left-paren> = <'('>
<right-paren> = <')'>
<left-bracket> = <'['>
<right-bracket> = <']'>
<left-curly> = <'{'>
<right-curly> = <'}'>

special-identifier = def-keyword | fn-keyword | do-keyword | if-keyword

nil = <'nil'>

integer = #'[+-]?\d+'
real = #'[+-]?\d+\.\d*'
<number> = (integer | real)

<double-quote> = '\"'
string = <double-quote> #'(?:[^"\\]|\\.)*' <double-quote>

regex = <'#'> <double-quote> #'(?:[^"\\]|\\.)*' <double-quote>

<symbol-text> = #'[^\s"()\[\]{}\d:/#][^\s"()\[\]{}/#]*'
symbol = symbol-text

<identifier-text> = #'[^\s"()\[\]{}\d:/\.#][^\s"()\[\]{}/\.#]*'
identifier = identifier-text
<namespace> = symbol-text
qualified-identifier = namespace <'/'> identifier-text


<keyword-text> = #'[^\s"()\[\]{}/:]+'
qualified-keyword = #'::?' keyword-text '/' keyword-text
keyword = #'::?' keyword-text

boolean = 'true' | 'false'

map = left-curly atom* right-curly
set = <'#'> left-curly atom* right-curly
vector = left-bracket atom* right-bracket

<def-keyword> = <'def'>
binding-definition = left-paren def-keyword identifier atom right-paren

<fn-keyword> = <'fn'>
argument-list = left-bracket identifier* right-bracket
fn-definition =
  left-paren fn-keyword identifier? argument-list
    atom*
  right-paren

<do-keyword> = <'do'>
do-definition =
  left-paren do-keyword
    atom*
  right-paren

<if-keyword> = <'if'>
if =
  left-paren if-keyword atom
    atom
    atom?
  right-paren

application =
  left-paren !special-identifier
    atom+
  right-paren
