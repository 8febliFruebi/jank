<program> = atom (ws atom)* ws*
<atom> = ws /
         regex / string / nil / number / boolean / set / map / vector /
         qualified-keyword / keyword / qualified-identifier / identifier /
         def /
         fn / do / let /
         if /
         application

<left-paren> = <'('>
<right-paren> = <')'>
<left-bracket> = <'['>
<right-bracket> = <']'>
<left-curly> = <'{'>
<right-curly> = <'}'>

<whitespace> = #'\s+' | comment+
<comment> = whitespace* #';.*'
<ws> = <whitespace>

special-identifier = def-keyword | fn-keyword | do-keyword | if-keyword | let-keyword

nil = <'nil'>

integer = #'[+-]?\d+'
real = #'[+-]?\d+\.\d*'
<number> = (integer | real)

<double-quote> = '\"'
string = <double-quote> #'(?:[^"\\]|\\.)*' <double-quote>

regex = <'#'> <double-quote> #'(?:[^"\\]|\\.)*' <double-quote>

<symbol-text> = #'[^\s"()\[\]{}\d:/#][^\s"()\[\]{}/#]*'

<identifier-text> = !integer !nil !boolean #'[^\s"()\[\]{}\d:/\.#][^\s"()\[\]{}/\.#]*'
identifier = '/' | identifier-text
<namespace> = symbol-text
qualified-identifier = namespace <'/'> identifier-text

<keyword-text> = #'[^\s"()\[\]{}/:]+'
qualified-keyword = #'::?' keyword-text '/' keyword-text
keyword = #'::?' keyword-text

boolean = 'true' | 'false'

map = left-curly atom* right-curly
set = <'#'> left-curly atom* right-curly
vector = left-bracket atom* right-bracket

<def-keyword> = <'def'>
def = left-paren ws* def-keyword ws identifier ws atom ws* right-paren

<fn-keyword> = <'fn'>
argument-list = left-bracket ws* identifier? (ws identifier)* ws* right-bracket
fn =
  left-paren ws* fn-keyword ws (identifier ws)? argument-list
    (ws atom)*
  ws* right-paren

<do-keyword> = <'do'>
do =
  left-paren do-keyword
    atom*
  right-paren

<if-keyword> = <'if'>
if =
  left-paren ws* if-keyword ws atom
    ws atom
    (ws atom)?
  ws* right-paren

<let-keyword> = <'let'>
let-bindings =
  left-bracket
    (ws* identifier ws atom)*
  ws* right-bracket
let =
  left-paren ws* let-keyword ws*
    let-bindings
    (ws atom)*
  ws* right-paren

application =
  left-paren
    ws* !special-identifier atom
    (ws atom)*
  ws* right-paren
