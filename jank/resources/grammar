<program> = atom*
<atom> = string / number / boolean /
         identifier / keyword /
         lambda-definition /
         macro-definition /
         binding-definition /
         if-statement /
         declare-statement /
         function-call

integer = #'[+-]?\d+'
real = #'[+-]?\d+\.\d*'
<number> = (integer | real)

<double-quote> = '\"'
<not-double-quote> = #'[^\"]*'
string = <double-quote> not-double-quote <double-quote>

identifier = #'[^\s"\'()\d][^\s"\'()]*' generic-specialization?
keyword = <':'> identifier

boolean = 'true' | 'false'

<left-paren> = <'('>
<right-paren> = <')'>
list = left-paren atom* right-paren

<lambda-keyword> = <'lambda'> | <'λ'>
argument-list = list
<return-list> = list
lambda-definition =
  left-paren lambda-keyword argument-list return-list
    atom*
  right-paren

specialization-list = left-paren
                        (type | specialization-list)*
                      right-paren
<generic-specialization> = <':'> specialization-list
function-call =
  left-paren identifier
    atom*
  right-paren

function-parameter-type = left-paren type* right-paren
function-return-type = left-paren type* right-paren
<function-signature> = left-paren
                         function-parameter-type
                         function-return-type
                       right-paren
function-type = ("ƒ" | "function") <":"> function-signature
type = (identifier | function-type)

<macro-keyword> = <'macro'>
<generic-list> = list
macro-definition =
  left-paren macro-keyword identifier generic-list argument-list
    atom*
  right-paren

<bind-keyword> = <'bind'>
binding-definition = left-paren bind-keyword identifier atom right-paren

<if-keyword> = <'if'>
condition = atom
then = atom
else = atom
if-statement = left-paren if-keyword condition
                 then
                 else?
               right-paren

<declare-keyword> = <'declare'>
declare-statement = left-paren declare-keyword
                      (type / (identifier type))
                    right-paren
