<program> = atom*
<atom> = string / number / boolean /
         generic-specialization /
         identifier / keyword /
         function-definition /
         macro-definition /
         binding-definition /
         function-call /
         list

integer = #'[+-]?\d+'
real = #'[+-]?\d+\.\d*'
<number> = (integer | real)

<double-quote> = '\"'
<not-double-quote> = #'[^\"]*'
string = <double-quote> not-double-quote <double-quote>

identifier = #'[^\s"\'()\d][^\s"\'()]*'
keyword = <':'> identifier

boolean = 'true' | 'false'

<left-paren> = <'('>
<right-paren> = <')'>
list = left-paren atom* right-paren

<function-keyword> = <'function'> | <'Æ’'>
<argument-list> = list
<return-list> = list
function-definition =
  left-paren function-keyword identifier argument-list return-list
    atom*
  right-paren

generic-specialization = <':'> list
function-call =
  left-paren identifier generic-specialization?
    atom*
  right-paren

<macro-keyword> = <'macro'>
<generic-list> = list
macro-definition =
  left-paren macro-keyword identifier generic-list argument-list
    atom*
  right-paren

<bind-keyword> = <'bind'>
binding-definition = left-paren bind-keyword identifier atom right-paren
